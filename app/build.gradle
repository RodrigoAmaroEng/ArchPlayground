apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../version.gradle'

jacoco {
    toolVersion = '0.8.2'
}

android {
    jacoco {
        version = '0.8.2'
    }
    compileSdkVersion 28
    testOptions.unitTests.includeAndroidResources = true
    testOptions.unitTests.returnDefaultValues = true
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
    defaultConfig {
        applicationId "br.eng.rodrigoamaro.architectureplayground"
        minSdkVersion 15
        targetSdkVersion 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            all {
                jvmArgs '-noverify', '-ea'
            }
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1"
    implementation 'org.koin:koin-android:1.0.2'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0-alpha07'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.1.1'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.squareup.retrofit2:converter-wire:2.4.0'
    implementation 'com.squareup.wire:wire-runtime:2.3.0-RC1'
    testImplementation 'io.mockk:mockk:1.8.12.kotlin13'
    androidTestImplementation 'androidx.test.ext:junit:1.0.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test:core:1.0.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'io.mockk:mockk-android:1.8.12.kotlin13'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
    androidTestUtil 'androidx.test:orchestrator:1.1.0'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['connectedAndroidTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$dispatch$*.*']
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/**/*.ec'
    ])
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}
apply from: '../syntax.gradle'
